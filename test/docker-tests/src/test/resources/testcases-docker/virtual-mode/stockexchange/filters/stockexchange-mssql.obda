[PrefixDeclaration]
xsd:		http://www.w3.org/2001/XMLSchema#
owl:		http://www.w3.org/2002/07/owl#
owl2xml:		http://www.w3.org/2006/12/owl2-xml#
:		http://www.owl-ontologies.com/Ontology1207768242.owl#
rdf:		http://www.w3.org/1999/02/22-rdf-syntax-ns#
rdfs:		http://www.w3.org/2000/01/rdf-schema#


[MappingDeclaration] @collection [[
mappingId	mapping-addresses
target		:address-{id} a :Address ; :addressID {id} ; :inStreet {street} ; :hasNumber {number} ; :inCity {city} ; :inState {state} ; :inCountry {country} .
source		select id, street, number, city, state, country from address

mappingId	mapping-person
target		:person-{id} a :PhysicalPerson ; :firstName {name} ; :lastName {lastname} ; :dateOfBirth {dateofbirth} ; :ssn {ssn} ; :hasAddress :address-{addressid} .
source		select * from person

mappingId	mapping-brokers
target		:person-{id} a :StockBroker  .
source		select id from broker

mappingId	mapping-investors
target		:person-{id} a :Investor .
source		select id from client

mappingId	mapping-companies
target		:company-{id} a :Company ; :companyName {name} ; :marketShares {marketshares} ; :netWorth {networth} .
source		select id, name, marketshares, networth from company

mappingId	mapping-company-addresses
target		:company-{id} :hasAddress :address-{addressid} .
source		select id, addressid from company

mappingId	mapping-stocks
target		:stock-{id} a :Stock ; :financialInstrumentID {id} ; :amountOfShares {numberofshares} ; :typeOfShares {sharetype} ; :stockDescription {description}@en-us .
source		select id, numberofshares, sharetype, description from stockinformation

mappingId	mapping-stockbooks
target		:stockbook-{date} a :StockExchangeList ; :listdate {date} .
source		select date from stockbooklist

mappingId	mapping-tradesOnBehalfOf-physical
target		:person-{brokerid} :tradesOnBehalfOf :person-{clientid} .
source		select brokerid, clientid from brokerworksfor

mappingId	mapping-tradesOnBehalfOf-legal
target		:person-{brokerid} :tradesOnBehalfOf :company-{companyid} .
source		select brokerid, companyid from brokerworksfor

mappingId	mapping-transactions
target		:transaction-{id} a :Transaction ; :transactionID {id} ; :transactionDate {date} ; :amountOfTransaction {amount} .
source		select id, date, amount from transactions

mappingId	mapping-transaction-roles-persons
target		:transaction-{id} :isExecutedBy :person-{brokerid} ; :isExecutedFor :person-{forclientid} ; :involvesInstrument :stock-{stockid} .
source		select id, brokerid, forclientid, stockid from transactions

mappingId	mapping-transaction-roles-companies
target		:transaction-{id} :isExecutedBy :person-{brokerid} ; :isExecutedFor :company-{forcompanyid} ; :involvesInstrument :stock-{stockid} .
source		select id, brokerid, forcompanyid, stockid from transactions

mappingId	mapping-stock-company
target		:stock-{id} :belongsToCompany :company-{companyid} .
source		select id, companyid from stockinformation

mappingId	mapping-stock-listedin
target		:stockbook-{date} :listsStock :stock-{stockid} .
source		select date, stockid from stockbooklist

mappingId	mapping-dealer
target		:person-{clientid} a :Dealer .
source		select clientid from broker,client,brokerworksfor where ( ( client.id = broker.id and brokerid=broker.id ) and client.id=clientid )

mappingId	mapping-offers
target		:transaction-{id} a :Offer .
source		SELECT id FROM transactions WHERE type=true
]]


